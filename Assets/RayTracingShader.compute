#pragma kernel CSMain
RWTexture2D<float4> Result;
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
float4 _DirectionalLight;
float2 _PixelOffset;
float2 _Pixel;
float _Seed;
Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;
static const float PI = 3.14159265f;

// function for generating random numbers from a seed
float rand()
{
    float result = frac(sin(_Seed / 100.0f * dot(_Pixel, float2(12.9898f, 78.233f))) * 43758.5453f);
    _Seed += 1.0f;
    return result;
}

float3x3 GetTangentSpace(float3 normal)
{
    // Choose a helper vector for the cross product
    float3 helper = float3(1, 0, 0);
    if (abs(normal.x) > 0.99f)
        helper = float3(0, 0, 1);
    // Generate vectors
    float3 tangent = normalize(cross(normal, helper));
    float3 binormal = normalize(cross(normal, tangent));
    return float3x3(tangent, binormal, normal);
}

float3 SampleHemisphere(float3 normal, float alpha)
{
    // Sample the hemisphere, where alpha determines the kind of the sampling
    float cosTheta = pow(rand(), 1.0f / (alpha + 1.0f));
    float sinTheta = sqrt(1.0f - cosTheta * cosTheta);
    float phi = 2 * PI * rand();
    float3 tangentSpaceDir = float3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);
    // Transform direction to world space
    return mul(tangentSpaceDir, GetTangentSpace(normal));
}

float sdot(float3 x, float3 y, float f = 1.0f)
{
    return saturate(dot(x, y) * f);
}

float energy(float3 color)
{
    return dot(color, 1.0f / 3.0f);
}

float SmoothnessToPhongAlpha(float s)
{
    return pow(1000.0f, s * s);
}



struct Sphere
{
    float3 position;
    float radius;
    float3 albedo;
    float3 specular;
    float smoothness;
    float3 emission;
};
StructuredBuffer<Sphere> _Spheres;
//-------------------------------------
//- RAY
struct Ray
{
    float3 origin;
    float3 direction;
    float3 energy;
};
Ray CreateRay(float3 origin, float3 direction)
{
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    ray.energy = float3(1.0f, 1.0f, 1.0f);
    return ray;
}
Ray CreateCameraRay(float2 uv)
{
    // Transform the camera origin to world space
    float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    
    // Invert the perspective projection of the view-space position
    float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    // Transform the direction from camera to world space and normalize
    direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);
    return CreateRay(origin, direction);
}
//-------------------------------------
//- RAYHIT
struct RayHit
{
    float3 position;
    float distance;
    float3 normal;
    float3 albedo;
    float3 specular;
    float smoothness;
    float3 emission;
};
RayHit CreateRayHit()
{
    RayHit hit;
    hit.position = float3(0.0f, 0.0f, 0.0f);
    hit.distance = 1.#INF;
    hit.normal = float3(0.0f, 0.0f, 0.0f);
    hit.albedo = float3(0.0f, 0.0f, 0.0f);
    hit.specular = float3(0.0f, 0.0f, 0.0f);
    hit.smoothness = 0.0f;
    hit.emission = float3(0.0f, 0.0f, 0.0f);
    return hit;
}
//-------------------------------------
//- INTERSECTION
void IntersectGroundPlane(float3 planeNormal, float planeOffset, float3 colour, Ray ray, inout RayHit bestHit)
{
    // Calculate distance along the ray where the plane is intersected
    float denominator = dot(planeNormal, ray.direction);
    if (abs(denominator) > 0.0001f) // Ensure the ray is not parallel to the plane
    {
        float t = (planeOffset - dot(planeNormal, ray.origin)) / denominator;
        if (t > 0 && t < bestHit.distance)
        {
            bestHit.distance = t;
            bestHit.position = ray.origin + t * ray.direction;
            
            // Determine the direction of the normal based on the angle with the light direction
            float3 lightDirection = normalize(ray.direction);
            float angle = dot(planeNormal, lightDirection);
            bestHit.normal = angle > 0 ? -normalize(planeNormal) : normalize(planeNormal);

            // For simplicity, you might also pass these parameters to the function
            bestHit.albedo = colour; // Example albedo
            bestHit.specular = float3(0.03f, 0.03f, 0.03f); // Example specular
            bestHit.smoothness = 0.2f; // Example smoothness
            bestHit.emission = float3(0.0f, 0.0f, 0.0f); // No emission
        }
    }
}
void IntersectSphere(Ray ray, inout RayHit bestHit, uint sphereIndex)
{
    // Calculate distance along the ray where the sphere is intersected
    Sphere sphere = _Spheres[sphereIndex];
    float3 d = ray.origin - sphere.position;
    float p1 = -dot(ray.direction, d);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.radius * sphere.radius;
    if (p2sqr < 0)
        return;
    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = normalize(bestHit.position - sphere.position);
        bestHit.albedo = sphere.albedo;
        bestHit.specular = sphere.specular;
        bestHit.smoothness = sphere.smoothness;
        bestHit.emission = sphere.emission;
    }
}
//-------------------------------------
//- TRACE
RayHit Trace(Ray ray)
{
    RayHit bestHit = CreateRayHit();
    // Trace ground plane
//     IntersectGroundPlane(float3(1.0f, 0.5f, 0.0f), 0.0f, ray, bestHit);
//     IntersectGroundPlane(float3(0.0f, 1.0f, 0.0f), 5.0f, ray, bestHit);
//    // IntersectGroundPlane(float3(0.0f, 1.0f, 0.0f), 0.0f, ray, bestHit);
    
//     IntersectGroundPlane(float3(0.0f, 0.0f, 1.0f), 5.0f, ray, bestHit);
//     IntersectGroundPlane(float3(0.0f, 0.0f, 1.0f), -5.0f, ray, bestHit);
    
//     IntersectGroundPlane(float3(0.0f, 0.0f, 1.0f), 10.0f, ray, bestHit);

IntersectGroundPlane(float3(1.0f, 0.0f, 0.0f), 0.0f, float3(0.0f, 1.0f, 0.0f), ray, bestHit);
IntersectGroundPlane(float3(0.0f, 1.0f, 0.0f), 6.0f, float3(0.8f, 0.8f, 0.8f), ray, bestHit);
IntersectGroundPlane(float3(0.0f, 1.0f, 0.0f), 0.0f, float3(0.8f, 0.8f, 0.8f), ray, bestHit);
IntersectGroundPlane(float3(0.0f, 0.0f, 1.0f), -5.0f, float3(1.0f, 0.0f, 0.0f), ray, bestHit);
IntersectGroundPlane(float3(0.0f, 0.0f, 1.0f), 5.0f, float3(0.0f, 0.0f, 1.0f), ray, bestHit);
//IntersectGroundPlane(float3(0.0f, 1.0f, 0.0f), 10.0f, ray, bestHit);

    // Trace spheres
    uint numSpheres, stride;
    _Spheres.GetDimensions(numSpheres, stride);
    for (uint i = 0; i < numSpheres; i++)
        IntersectSphere(ray, bestHit, i);
    return bestHit;
}
//-------------------------------------
//- SHADE
float3 Shade(inout Ray ray, RayHit hit)
{
    if (hit.distance < 1.#INF)
    {
    // Calculate chances of diffuse and specular reflection
    hit.albedo = min(1.0f - hit.specular, hit.albedo);
    float specChance = energy(hit.specular);
    float diffChance = energy(hit.albedo);
    float sum = specChance + diffChance;
    specChance /= sum;
    diffChance /= sum;
    // Roulette-select the ray's path
    float roulette = rand();
    if (roulette < specChance)
    {
        // Specular reflection
        float alpha = SmoothnessToPhongAlpha(hit.smoothness);
        ray.origin = hit.position + hit.normal * 0.001f;
        ray.direction = SampleHemisphere(reflect(ray.direction, hit.normal), alpha);
        float f = (alpha + 2) / (alpha + 1);
        ray.energy *= (1.0f / specChance) * hit.specular * sdot(hit.normal, ray.direction, f);
    }
    else
    {
        // Diffuse reflection
        ray.origin = hit.position + hit.normal * 0.001f;
        ray.direction = SampleHemisphere(hit.normal, 1.0f);
        ray.energy *= (1.0f / diffChance) * hit.albedo;
    }
    }
    else
    {
        // Erase the ray's energy - the sky doesn't reflect anything
        ray.energy = 0.0f;
        // Sample the skybox and write it
        // float theta = acos(ray.direction.y) / -PI;
        // float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.5f;
        // return _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz * 0.7f;
    }
    return hit.emission;
}
//-------------------------------------
//- KERNEL
[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    // Get the dimensions of the RenderTexture
    uint width, height;
    Result.GetDimensions(width, height);
    // Transform pixel to [-1,1] range
    float2 uv = float2((id.xy + _PixelOffset) / float2(width, height) * 2.0f - 1.0f);

    _Pixel = id.xy;

    // Get a ray for the UVs
    Ray ray = CreateCameraRay(uv);
    // Trace and shade the ray
    float3 result = float3(0, 0, 0);
    for (int i = 0; i < 16; i++)
    {
        RayHit hit = Trace(ray);
        result += ray.energy * Shade(ray, hit);
        if (!any(ray.energy))
            break;
    }
    Result[id.xy] = float4(result, 1);
}